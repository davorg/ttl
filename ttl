#!/usr/bin/perl

use strict;
use warnings;
use feature 'say';

use File::Basename;
use Getopt::Std;
use List::Util 'first';

my %opts;
getopts('hst:', \%opts);

usage() if $opts{h};

my $domain = shift;

die "Give me a domain\n" unless defined $domain;

my $show_secs;
$show_secs = 1 if $opts{s};

my $cmd = "dig $domain";

if ($opts{t}) {
  $cmd .= " $opts{t}";
}

my ($line) = first { /$domain\.\s+\d+/ } qx[$cmd];

my ($dur_secs) = $line =~ /\s+(\d+)/;

if ($show_secs) {
  say $dur_secs;
  exit;
}

my $secs = $dur_secs % 60;

my $dur_mins = int ($dur_secs / 60);
my $mins = $dur_mins % 60;

my $hrs = int ($dur_mins / 60);

say sprintf '%02d:%02d:%02d', $hrs, $mins, $secs;

sub usage {
  my $me = basename $0;

  say <<"EOT";
Usage:

  \$ $me [-s] [t type] [-h] domain

* -s: Show TTL in seconds (rather the default HH:MM:SS format)
* -t: Give a DNS record type to query for the domain
* -h: Display this help message
EOT

  exit;
}
